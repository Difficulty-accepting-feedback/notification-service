plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.3'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'org.asciidoctor.jvm.convert' version '3.3.2'
	id 'jacoco'
}

group = 'com.grow'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

jacoco {
	toolVersion = "0.8.13"
}

def jacocoExcludes = [
		'**/repository/**',
		'**/controller/**',
		'**/exception/**',
		'**/dto/**',
		'**/thirdparty/**',
		'**/entity/**',
		'**/Q[A-Z]*.class',
		'**/config/**',
		'**/common/**',
		'**/constants/**',
		'**/listener/**',
		'**/swagger/**',
		'**/NotificationServiceApplication*',
		'**/client/**',
		'**/analysis/**',
		'**/event/**',
		'**/kafka/**',
		'**/slack/**',
		'**/util/**',
		'**/seed/**',
		'**/analysis/**'

]

jacocoTestCoverageVerification {
	afterEvaluate {
		classDirectories.setFrom(
				files(classDirectories.files.collect { dir ->
					fileTree(dir: dir, excludes: jacocoExcludes)
				})
		)
	}
	violationRules {
		rule {
			enabled = true
			element = 'CLASS'
			limit {
				counter = 'LINE' // 라인 커버리지 기준
				value = 'COVEREDRATIO'
				minimum = 0.70 // 70% 이상이어야 통과
			}
		}
	}
}

jacocoTestReport {
	reports {
		html.required.set(true)
		html.destination file(project.layout.buildDirectory.dir("jacoco/index.html")) as File
	}
	afterEvaluate {
		classDirectories.setFrom(
				files(classDirectories.files.collect { dir ->
					fileTree(dir: dir, excludes: jacocoExcludes)
				})
		)
	}
	finalizedBy(jacocoTestCoverageVerification)
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

ext {
	set('snippetsDir', file("build/generated-snippets"))
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	compileOnly 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	runtimeOnly 'com.h2database:h2'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'

	// feign
	implementation platform("org.springframework.cloud:spring-cloud-dependencies:2025.0.0")
	implementation "org.springframework.cloud:spring-cloud-starter-openfeign"

	// Actuator + AOP 추가 메트릭스 모니터링
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-aop'

	// resilience4j 추가
	implementation 'io.github.resilience4j:resilience4j-all:2.3.0'
	implementation 'io.github.resilience4j:resilience4j-spring-boot3'

	// webClient
	implementation 'org.springframework.boot:spring-boot-starter-webflux'

	// Swagger
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.6'
	implementation 'com.danielfrak.code:springdoc-openapi-externalized-documentation:1.0.0'

	// kafka
	implementation 'org.springframework.kafka:spring-kafka'
	testImplementation "org.springframework.kafka:spring-kafka-test"

	// slack
	implementation 'com.slack.api:slack-api-client:1.43.0'

	// json 타입을 rdbms에 저장하기 위한 hibernate-types
	implementation("com.vladmihalcea:hibernate-types-60:2.21.1")

	// 제미나이 AI
	implementation "com.google.genai:google-genai:1.16.0"
}

tasks.named('test') {
	outputs.dir snippetsDir
	useJUnitPlatform()
}

tasks.named('asciidoctor') {
	inputs.dir snippetsDir
	dependsOn test
}

tasks.withType(JavaCompile) {
	options.compilerArgs += ['-parameters']
}

test {
	finalizedBy 'jacocoTestReport'
}

configurations.all {
	exclude group: 'commons-logging', module: 'commons-logging'
}